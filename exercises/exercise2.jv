pipeline Neuss_Tree_Pipeline {

    block Data_Extractor oftype HttpExtractor {
        url: "https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/stadt-neuss-herbstpflanzung-2023/exports/csv";
    }

    block TextFileInterpreter oftype TextFileInterpreter { }

    block CSV_Parser oftype CSVInterpreter {
        delimiter: ';';
    }

    block Column_Remover oftype ColumnDeleter {
        delete: [column E];
    }

    valuetype Valid_Geo_Pattern oftype text {
        constraints: [
            Geo_Pattern_Constraint
        ];
    }

    constraint Geo_Pattern_Constraint oftype RegexConstraint {
        regex: /^\d{1,3}\.\d+, \d{1,3}\.\d+$/;
    }

    valuetype Furth_Stadtteil oftype text {
        constraints: [
            Furth_Constraint
        ];
    }

    constraint Furth_Constraint oftype RegexConstraint {
        regex: /^Furth-.*/;
    }

    block Data_Interpreter oftype TableInterpreter {
        header: true;
        columns: [
            "lfd_nr" oftype integer,
            "stadtteil" oftype Furth_Stadtteil,
            "standort" oftype text,
            "baumart_botanisch" oftype text,
            "baumart_deutsch" oftype text,
            "id" oftype Valid_Geo_Pattern,            
            "baumfamilie" oftype text
        ];
    }

    block Data_Loader oftype SQLiteLoader {
        table: "trees";
        file: "trees.sqlite";
    }

    Data_Extractor -> TextFileInterpreter
                   -> CSV_Parser
                   -> Column_Remover
                   -> Data_Interpreter
                   -> Data_Loader;
}