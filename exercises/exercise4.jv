pipeline TemperaturePipeline {

    // Download the ZIP file from the given URL
    block DownloadZip oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    //  Extract the ZIP file
    block Unzip_File oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    //  Locate the desired CSV file in the extracted folder
    block Pick_CSV oftype FilePicker {
        path: "/data.csv";
    }

    //  Read the contents of the CSV file as plain text
    block Read_Raw_Data oftype TextFileInterpreter {}

    // Parse the raw text into structured CSV data
    block Parse_CSV oftype CSVInterpreter {
        delimiter: ";";
    }

    //  Rename the battery temperature column header
    block Rename_Battery oftype CellWriter {
        at: cell J1;
        write: ["battery_temperature"];
    }

//  Apply the same transformation to the "battery_temperature" column
    block Convert_Battery_Temp oftype TableTransformer {
        inputColumns: ["battery_temperature"];
        outputColumn: "battery_temperature";
        uses: CelsiusToFahrenheit;
    }

    //  Define a transformation for converting Celsius to Fahrenheit
    transform CelsiusToFahrenheit {
        from TempCelsius oftype decimal;
        to TempFahrenheit oftype decimal;

        TempFahrenheit: 32 + ((TempCelsius / 5) * 9);
    }

    //  Apply the Celsius-to-Fahrenheit transformation to the "temperature" column
    block Convert_Temp oftype TableTransformer {
        inputColumns: ["temperature"];
        outputColumn: "temperature";
        uses: CelsiusToFahrenheit;
    }

//  Rename the main column headers from German to English
    block Rename_Headers oftype CellWriter {
        at: range A1:E1;
        write: ["id", "producer", "model", "month", "temperature"];
    }

    //  Save the transformed and validated data into an SQLite database
    block Save_Data oftype SQLiteLoader {
        table: "temperatures";
        file: "./temperatures.sqlite";
    }
//  Validate the structure and types of the data columns
    block Validate_Data oftype TableInterpreter {
        header: true;
        columns: [
            "id" oftype integer,
            "producer" oftype text,
            "model" oftype text,
            "month" oftype integer,
            "temperature" oftype decimal,
            "battery_temperature" oftype decimal
        ];
    }

    // Define the execution flow of the pipeline
    DownloadZip 
        -> Unzip_File
        -> Pick_CSV 
        -> Read_Raw_Data
        -> Parse_CSV
        -> Rename_Headers
        -> Rename_Battery
        -> Validate_Data
        -> Convert_Temp
        -> Convert_Battery_Temp
        -> Save_Data;
}
